/////////////////////////////////////////////////////
//    NIPS Shift Return for Team Leader            //
/////////////////////////////////////////////////////


Datamodel ShftDet  "NIPS Shift Return"

LANGUAGES =
    ENG   "English"

PRIMARY
    Site,
    ShftDate,
    ampm,
    TeamL
    
ROLES =  
    Help "Help",
    Watermark "Data entry watermark",
    TEMPLATE "To apply templates",
    HIDECATEGORY "To hide answer category based on routing",
    EDITMASK

Type
   Calendar = datetype
   
AUXFIELDS
      HIDECATAIR:string[10]
      HIDECATSTENA:string[10]
      HIDECATLPL:string[10]
      HIDECATLAR:string[10]


// Each interviewers details
Block BIntDet "Team deatils"
       FIELDS
          Name    "Name" Watermark "Name":string[20], empty

//          StartT  "Start Time" :{EDITMASK "99/:99": string[6]}
//          EndT    "Finish Time" : timetype
          Ints    "Number of interviews completed today"/"Interviews": 0..999,empty
   
//TimeType
Group GTimeS description "Start Time"
   FIELDS
      StHour Watermark "HH": 0..24,EMPTY
      Stminutes Watermark "MM":0..60,EMPTY
      StartTime :Timetype
   RULES
      StHour
      StMinutes
      StartTime.keep
      StartTime := ToTime(StHour,Stminutes,0)
EndGroup

Group GTimeEnd Description "End Time"
   FIELDS
      EndHour Watermark "HH": 0..24,EMPTY
      
      Endminutes Watermark "MM":0..60,EMPTY
      EndTime :Timetype
   RULES
      EndHour
      EndMinutes
      EndTime.keep
      EndTime := ToTime(EndHour,Endminutes,0)
EndGroup

       RULES
          Name
//         if Name=response then
              GTimeS
              GTimeEnd
              Ints
//         ENDIF
 EndBLOCK {BIntDet}

Group GIntDet
    Locals
       i: integer

    Fields
       BInt: Array [1..12] of BIntDet
    Rules
        For i:=1 to Ints do
            BInt[i].ask
        Enddo
EndGroup


Locals
    MissInt: integer

Fields
    Site        "Site/port working in?": 
                     (BFS "Belfast International", 
                     BHD "Belfast City", 
                     LDY "City of Derry", 
                     StenC "Stena Cairnryan", 
                     StenL "Stena Liverpool", 
                     Larne)
       ampm        "Shift times?"
                   TEMPLATE "Button_x2_nogrp":
                    (am HIDECATEGORY "^{HIDECATAIR}",
                     pm HIDECATEGORY "^{HIDECATAIR}",
                     SC1 "0730 Boat <NEWLINE><grey>(Stena Cairnryan)</grey>" HIDECATEGORY "^{HIDECATSTENA}",
                     SC2 "1130 Boat <NEWLINE><grey>(Stena Cairnryan)</grey>" HIDECATEGORY "^{HIDECATSTENA}",
                     SC3 "1530 Boat <NEWLINE><grey>(Stena Cairnryan)</grey>" HIDECATEGORY "^{HIDECATSTENA}",
                     SC4 "1930 Boat <NEWLINE><grey>(Stena Cairnryan)</grey>" HIDECATEGORY "^{HIDECATSTENA}",
                     SC5 "2300/2330 Boat <NEWLINE><grey>(Stena Cairnryan)</grey>" HIDECATEGORY "^{HIDECATSTENA}",
                     SLa "am Boat <NEWLINE><grey>(Stena Liverpool)</grey>" HIDECATEGORY "^{HIDECATLPL}",
                     SLp "pm Boat <NEWLINE><grey>(Stena Liverpool)</grey>" HIDECATEGORY "^{HIDECATLPL}",
                     LA  "sailings between 0600 - 1030<newline><grey>(Larne port)</grey>" HIDECATEGORY "^{HIDECATLAR}",
                     LB  "sailings between 1100 - 1630<newline><grey>(Larne port)</grey>" HIDECATEGORY "^{HIDECATLAR}",
                     LC  "sailings between 1800 - 2359<newline><grey>(Larne port)</grey>"HIDECATEGORY "^{HIDECATLAR}")
                     
    Sample      "Sampling interval...": (five "1 in 5", ten "1 in 10")
    ShftDate    "Shift Date" TEMPLATE "HORIZONTAL": Calendar
    TeamL       "Team Leaders or Interviewer Authno? <newline><green>Interviewers should only complete this form if asked or need to make a 
                  report about an incident during shift. Please phone survey section as well.</green>"
//               TEMPLATE "Horizontal"
               :1..9999
    IntRep     "": (FT "Field Trainer Report", Int "Interviewer Report")
    
    Day         "Day" TEMPLATE "Button_S": (Sunday, Monday, Tuesday, Wed "Wednesday", Thur "Thursday", Friday, Sat "Saturday")
 
    RandomSt    "Random start number used?" TEMPLATE "Horizontal":0..9, DK, RF
    Ints        "Number of interviewers working on shift (including yourself)?" TEMPLATE "Horizontal": 1..12
    
//    IntDet: GIntDet     
    
    Click1      "Clicker number 1 <green>(main clicker)</Green>?" 
                  WATERMARK "Main":0..9999, DK, RF
    Click2      "Clicker number 2 <Green>(2nd Clicker)</Green>?" 
                  WATERMARK "Second":0..9999, DK, RF, empty
    Clicks      "Total number of clicked passengers over all counting points.<newline>
               <Green> If clicks please sign and return sheet to the office</Green>":0..9999, DK, RF
   
//    {Include counter for all interviewers instead!}   
//    NoInts      "Did ANYONE NOT interview on their tablet, if so who?"
//                WATERMARK "Please enter names...": string[200], empty
    
    NOTES       "Please enter anything unusually from the shift or any problem/issues that occurred on the shift": open, empty
    CompInts    "Total number of interviews on interviewers machines":0..9999
    ClckInts    "Total number of interviews from clicker":0..9999
    Missing     "There are <Bold><Red>^MissInt</Bold></Red> missing interviews on interviewers machines when compared to the clicker number.<Bold>Please explain why?</Bold><newline>
                <Green>Note if this is a negitive number - that means you have too many interviews recorded.</Green>":string[100],empty

RULES
    Site.show
    if site = BFS then
        sample:=ten
    else
        sample :=five
    endif
    
    DAY.Keep
    if Shftdate=response then
        Day := weekday(shftdate)
    endif
    
    if site.ord<4 then
      HIDECATAIR := 'SHOW'
      HIDECATSTENA := 'HIDE'
      HIDECATLPL := 'HIDE'
      HIDECATLAR := 'HIDE'
   elseif site = stenC then
      HIDECATAIR := 'HIDE'
      HIDECATSTENA := 'SHOW'
      HIDECATLPL := 'HIDE'
      HIDECATLAR := 'HIDE'
   elseif site = stenL then
      HIDECATAIR := 'HIDE'
      HIDECATSTENA := 'HIDE'
      HIDECATLPL := 'SHOW'
      HIDECATLAR := 'HIDE'
   elseif site = Larne then
      HIDECATAIR := 'HIDE'
      HIDECATSTENA := 'HIDE'
      HIDECATLPL := 'HIDE'
      HIDECATLAR := 'SHOW'
   else
      HIDECATAIR := 'SHOW'
      HIDECATSTENA := 'SHOW'
      HIDECATLPL := 'SHOW'
      HIDECATLAR := 'SHOW'   
   ENDIF 
    
    sample.KEEP
    ShftDate.show
    ampm.show
    TeamL.show
    
    IF IntRep=empty and (TeamL=543 or TeamL=489 or TeamL=1043 or TeamL=635 or TeamL=1267 or TeamL=1254 or TeamL=1159 or TeamL=433) then 
      INTRep:=FT 
    endif
    
    IntRep
    If IntRep=FT then
       Ints
       GIntDet
       
       RandomSt
       Click1
       Click2
       clicks
     // noints
    ENDIF

    If sample=five then
        ClckInts := int(click1/5) + int(click2/5)
    Elseif sample=ten then
        ClckInts := int(click1/10) + int(click2/10)
    Endif
 
    compInts := BInt[1].Ints + BInt[2].Ints + BInt[3].Ints + BInt[4].Ints + BInt[5].Ints + BInt[6].Ints + BInt[7].Ints + BInt[8].Ints + BInt[9].Ints + BInt[10].Ints + BInt[11].Ints + BInt[12].Ints
    MissInt := ClckInts - compInts - clicks
    
    If (MissInt>1 or MissInt<-1) and compInts>0 then
        Missing
    ENDIF
    Notes

Endmodel